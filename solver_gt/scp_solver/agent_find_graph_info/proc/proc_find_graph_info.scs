scp_program -> proc_find_graph_info
(*
	-> rrel_params: ...
	(*
		-> rrel_1: rrel_in: _solver_object;;
		-> rrel_2: rrel_in: _key_element;;
		-> rrel_3: rrel_in: _used_solver_elements;;
		-> rrel_4: rrel_in: _used_key_elements;;
		-> rrel_5: rrel_in: _solution_node;;
	*);;

	-> rrel_operators: ... 
	(*
		-> .proc_find_graph_info_operator001
		(*
			<- call;;
		    -> rrel_1: rrel_fixed: rrel_scp_const: proc_print_idtf;;
		    -> rrel_2: rrel_fixed: rrel_scp_const: ... 
				(*
					-> rrel_1: rrel_fixed: rrel_scp_var: _key_element;;
				*);;
		    -> rrel_3: rrel_assign: rrel_scp_var: _descr1;;

			=> nrel_goto: .proc_find_graph_info_operator002;;
		*);;

		-> .proc_find_graph_info_operator002
		(*
			<- waitReturn;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _descr1;;

			=> nrel_goto: .proc_find_graph_info_operator003;;
		*);;

		-> .proc_find_graph_info_operator003
		(*
			<- call;;
		    -> rrel_1: rrel_fixed: rrel_scp_const: proc_print_idtf;;
		    -> rrel_2: rrel_fixed: rrel_scp_const: ... 
				(*
					-> rrel_1: rrel_fixed: rrel_scp_var: _solver_object;;
				*);;
		    -> rrel_3: rrel_assign: rrel_scp_var: _descr1;;

			=> nrel_goto: .proc_find_graph_info_operator004;;
		*);;

		-> .proc_find_graph_info_operator004
		(*
			<- waitReturn;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _descr1;;

			=> nrel_goto: .proc_find_graph_info_operator01;;
		*);;

		-> rrel_init: .proc_find_graph_info_operator01
		(*
			<- genElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _used_key_elements;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _ara;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _key_element;;
			
			=> nrel_goto: .proc_find_graph_info_operator0;;
		*);;

		-> .proc_find_graph_info_operator0
		(*
			<- call;;
		    -> rrel_1: rrel_fixed: rrel_scp_const: proc_nodes_is_connected;;
		    -> rrel_2: rrel_fixed: rrel_scp_const: ... 
				(*
					-> rrel_1: rrel_fixed: rrel_scp_var: _solver_object;;
					-> rrel_2: rrel_fixed: rrel_scp_var: _key_element;;
					-> rrel_3: rrel_assign: rrel_scp_var: _isConnected;;
				*);;
		    -> rrel_3: rrel_assign: rrel_scp_var: _descr1;;

			=> nrel_goto: .proc_find_graph_info_operator1;;
		*);;

		-> .proc_find_graph_info_operator1
		(*
			<- waitReturn;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _descr1;;

			=> nrel_goto: .proc_find_graph_info_operator2;;
		*);;

        -> .proc_find_graph_info_operator2
		(*
			<- ifEq;;
		  	-> rrel_1: rrel_fixed: rrel_scp_var: _isConnected;;
		  	-> rrel_2: rrel_fixed: rrel_scp_const: [1];;

			=> nrel_then: .proc_find_graph_info_operator_return;;
			=> nrel_else: .proc_find_graph_info_operator3;;
		*);;

        -> .proc_find_graph_info_operator3
		(*
			<- searchSetStr5;;
    		-> rrel_1: rrel_assign: rrel_scp_var: _solver_element;;
    		-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
    		-> rrel_3: rrel_fixed: rrel_scp_var: _key_element;;
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
    		-> rrel_5: rrel_fixed: rrel_scp_const: rrel_solver_key_sc_element;;

			-> rrel_set_1: rrel_assign: rrel_scp_var: _solver_elements_set;;

			=> nrel_then: .proc_find_graph_info_operator4;;
			=> nrel_else: .proc_find_graph_info_operator_return;;
		*);;

		-> .proc_find_graph_info_operator4
		(*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _solver_elements_set;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc_erase_1;;
            -> rrel_3: rrel_assign: rrel_scp_var: _current_solver_element;;
			
			=> nrel_then: .proc_find_graph_info_operator6;;
			=> nrel_else: .proc_find_graph_info_operator5;;
		*);;

		-> .proc_find_graph_info_operator5
		(*
			<- eraseEl;;
            -> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _solver_elements_set;;

			=> nrel_goto: .proc_find_graph_info_operator_return;;
		*);;
		
		-> .proc_find_graph_info_operator6
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_erase: rrel_pos_const_perm: rrel_scp_var: _arc_erase_1;;
                        
			=> nrel_goto: .proc_find_graph_info_operator6_1;;
		*);;

		-> .proc_find_graph_info_operator6_1
		(*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _used_solver_elements;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
            -> rrel_3: rrel_fixed: rrel_scp_var: _current_solver_element;;
			
			=> nrel_then: .proc_find_graph_info_operator4;;
			=> nrel_else: .proc_find_graph_info_operator6_2;;
		*);;

		-> .proc_find_graph_info_operator6_2
		(*
			<- call;;
		    -> rrel_1: rrel_fixed: rrel_scp_const: proc_create_new_solution_node;;
		    -> rrel_2: rrel_fixed: rrel_scp_const: ... 
				(*
					-> rrel_1: rrel_fixed: rrel_scp_var: _solution_node;;
					-> rrel_2: rrel_fixed: rrel_scp_var: _key_element;;
					-> rrel_3: rrel_fixed: rrel_scp_var: _current_solver_element;;
					-> rrel_4: rrel_assign: rrel_scp_var: _new_solution_node;;
				*);;
		    -> rrel_3: rrel_assign: rrel_scp_var: _descr1;;

			=> nrel_goto: .proc_find_graph_info_operator6_3;;
		*);;

		-> .proc_find_graph_info_operator6_3
		(*
			<- waitReturn;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _descr1;;

			=> nrel_goto: .proc_find_graph_info_operator7;;
		*);;

		-> .proc_find_graph_info_operator7
		(*
			<- call;;
		    -> rrel_1: rrel_fixed: rrel_scp_const: proc_extract_init_and_result_sturct;;
		    -> rrel_2: rrel_fixed: rrel_scp_const: ... 
				(*
					-> rrel_1: rrel_fixed: rrel_scp_var: _current_solver_element;;
					-> rrel_2: rrel_fixed: rrel_scp_var: _key_element;;
					-> rrel_3: rrel_assign: rrel_scp_var: _init_struct;;
					-> rrel_4: rrel_assign: rrel_scp_var: _result_struct;;
				*);;
		    -> rrel_3: rrel_assign: rrel_scp_var: _descr1;;

			=> nrel_goto: .proc_find_graph_info_operator7_1_1;;
		*);;

		-> .proc_find_graph_info_operator7_1_1
		(*
			<- waitReturn;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _descr1;;

			=> nrel_goto: .proc_find_graph_info_operator7_1_2;;
		*);;

		-> .proc_find_graph_info_operator7_1_2
		(*
			<- ifVarAssign;;
			-> rrel_1: rrel_assign: rrel_scp_var: _init_struct;;

			=> nrel_then: .proc_find_graph_info_operator7_1_3;;
			=> nrel_else: .proc_find_graph_info_operator4;;
		*);;

		-> .proc_find_graph_info_operator7_1_3
		(*
			<- ifVarAssign;;
			-> rrel_1: rrel_assign: rrel_scp_var: _result_struct;;

			=> nrel_then: .proc_find_graph_info_operator7_1;;
			=> nrel_else: .proc_find_graph_info_operator4;;
		*);;

		-> .proc_find_graph_info_operator7_1
		(*
			<- call;;
		    -> rrel_1: rrel_fixed: rrel_scp_const: proc_find_var_in_solver_element;;
		    -> rrel_2: rrel_fixed: rrel_scp_const: ... 
				(*
					-> rrel_1: rrel_fixed: rrel_scp_var: _current_solver_element;;
					-> rrel_2: rrel_fixed: rrel_scp_var: _init_struct;;
					-> rrel_3: rrel_fixed: rrel_scp_var: _result_struct;;
					-> rrel_4: rrel_assign: rrel_scp_var: _var_from_solver_element;;
				*);;
		    -> rrel_3: rrel_assign: rrel_scp_var: _descr1;;

			=> nrel_goto: .proc_find_graph_info_operator7_2;;
		*);;

		-> .proc_find_graph_info_operator7_2
		(*
			<- waitReturn;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _descr1;;

			=> nrel_goto: .proc_find_graph_info_operator7_3;;
		*);;

		-> .proc_find_graph_info_operator7_3
		(*
			<- ifVarAssign;;
			-> rrel_1: rrel_assign: rrel_scp_var: _var_from_solver_element;;

			=> nrel_then: .proc_find_graph_info_operator8;;
			=> nrel_else: .proc_find_graph_info_operator4;;
		*);;


		-> .proc_find_graph_info_operator8
		(*
			<- sys_search;;	
			
			-> rrel_1: rrel_fixed: rrel_scp_var: _init_struct;;
		 	-> rrel_2: rrel_fixed: rrel_scp_const: ...;;
			-> rrel_3: rrel_fixed: rrel_scp_const: ... 
				(* -> ... 
					(* -> rrel_1: rrel_fixed: rrel_scp_var: _var_from_solver_element;;
                       -> rrel_2: rrel_fixed: rrel_scp_var: _solver_object;; 
					*);;
				*);;

			=> nrel_then: .proc_find_graph_info_operator9;;
			=> nrel_else: .proc_find_graph_info_operator12;;
		*);;

		-> .proc_find_graph_info_operator9
		(*
			<- call;;
		    -> rrel_1: rrel_fixed: rrel_scp_const: proc_use_solver_element;;
		    -> rrel_2: rrel_fixed: rrel_scp_const: ... 
				(*
					-> rrel_1: rrel_fixed: rrel_scp_var: _solver_object;;
					-> rrel_2: rrel_fixed: rrel_scp_var: _key_element;;
					-> rrel_3: rrel_fixed: rrel_scp_var: _current_solver_element;;
					-> rrel_4: rrel_fixed: rrel_scp_var: _new_solution_node;;
					-> rrel_5: rrel_fixed: rrel_scp_var: _init_struct;;
					-> rrel_6: rrel_fixed: rrel_scp_var: _result_struct;;
				*);;
		    -> rrel_3: rrel_assign: rrel_scp_var: _descr2;;

			=> nrel_goto: .proc_find_graph_info_operator10;;
		*);;

		-> .proc_find_graph_info_operator10
		(*
			<- waitReturn;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _descr2;;
			
			=> nrel_goto: .proc_find_graph_info_operator11;;
		*);;

		-> .proc_find_graph_info_operator11
		(*
			<- genElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _used_solver_elements;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _ara;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _current_solver_element;;

			=> nrel_goto: .proc_find_graph_info_operator5;;
		*);;

		-> .proc_find_graph_info_operator12
		(*
			<- searchSetStr3;;
    		-> rrel_1: rrel_fixed: rrel_scp_var: _init_struct;;
    		-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
    		-> rrel_3: rrel_assign: rrel_scp_var: rrel_const: _element;;

			-> rrel_set_3: rrel_assign: rrel_scp_var: _init_struct_copy;;

			=> nrel_then: .proc_find_graph_info_operator13;;
			=> nrel_else: .proc_find_graph_info_operator5;;
		*);;

		-> .proc_find_graph_info_operator13
		(*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _init_struct_copy;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc_erase_2;;
            -> rrel_3: rrel_assign: rrel_scp_var: _init_struct_element;;
			
			=> nrel_then: .proc_find_graph_info_operator15;;
			=> nrel_else: .proc_find_graph_info_operator14;;
		*);;

		-> .proc_find_graph_info_operator14
		(*
			<- eraseEl;;
            -> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _init_struct_copy;;

			=> nrel_goto: .proc_find_graph_info_operator14_1;;
		*);;

		-> .proc_find_graph_info_operator14_1
		(*
			<- sys_search;;	
			-> rrel_1: rrel_fixed: rrel_scp_var: _init_struct;;
		 	-> rrel_2: rrel_fixed: rrel_scp_const: ...;;
			-> rrel_3: rrel_fixed: rrel_scp_const: ... 
				(* -> ... 
					(* -> rrel_1: rrel_fixed: rrel_scp_var: _var_from_solver_element;;
                       -> rrel_2: rrel_fixed: rrel_scp_var: _solver_object;; 
					*);;
				*);;

			=> nrel_then: .proc_find_graph_info_operator9;;
			=> nrel_else: .proc_find_graph_info_operator4;;
		*);;
		
		-> .proc_find_graph_info_operator15
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_erase: rrel_pos_const_perm: rrel_scp_var: _arc_erase_2;;
                        
			=> nrel_goto: .proc_find_graph_info_operator16;;
		*);;

		-> .proc_find_graph_info_operator16
		(*
			<- searchElStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _current_solver_element;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
            -> rrel_3: rrel_fixed: rrel_scp_var: _init_struct_element;;
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
    		-> rrel_5: rrel_fixed: rrel_scp_const: rrel_key_sc_element;;
			
			=> nrel_then: .proc_find_graph_info_operator17;;
			=> nrel_else: .proc_find_graph_info_operator13;;
		*);;

		-> .proc_find_graph_info_operator17
		(*
			<- call;;
		    -> rrel_1: rrel_fixed: rrel_scp_const: proc_nodes_is_connected;;
		    -> rrel_2: rrel_fixed: rrel_scp_const: ... 
				(*
					-> rrel_1: rrel_fixed: rrel_scp_var: _solver_object;;
					-> rrel_2: rrel_fixed: rrel_scp_var: _init_struct_element;;
					-> rrel_3: rrel_assign: rrel_scp_var: _isConnected;;
				*);;
		    -> rrel_3: rrel_assign: rrel_scp_var: _descr1;;

			=> nrel_goto: .proc_find_graph_info_operator18;;
		*);;

		-> .proc_find_graph_info_operator18
		(*
			<- waitReturn;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _descr1;;

			=> nrel_goto: .proc_find_graph_info_operator19;;
		*);;

        -> .proc_find_graph_info_operator19
		(*
			<- ifEq;;
		  	-> rrel_1: rrel_fixed: rrel_scp_var: _isConnected;;
		  	-> rrel_2: rrel_fixed: rrel_scp_const: [1];;

			=> nrel_then: .proc_find_graph_info_operator13;;
			=> nrel_else: .proc_find_graph_info_operator20;;
		*);;

		-> .proc_find_graph_info_operator20
		(*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _used_key_elements;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
            -> rrel_3: rrel_fixed: rrel_scp_var: _init_struct_element;;
			
			=> nrel_then: .proc_find_graph_info_operator23;;
			=> nrel_else: .proc_find_graph_info_operator21;;
		*);;

		-> .proc_find_graph_info_operator21
		(*
			<- call;;
		    -> rrel_1: rrel_fixed: rrel_scp_const: proc_find_graph_info;;
		    -> rrel_2: rrel_fixed: rrel_scp_const: ... 
				(*
					-> rrel_1: rrel_fixed: rrel_scp_var: _solver_object;;
					-> rrel_2: rrel_fixed: rrel_scp_var: _init_struct_element;;
					-> rrel_3: rrel_fixed: rrel_scp_var: _used_solver_elements;;
					-> rrel_4: rrel_fixed: rrel_scp_var: _used_key_elements;;
					-> rrel_5: rrel_fixed: rrel_scp_var: _new_solution_node;;
				*);;
		    -> rrel_3: rrel_assign: rrel_scp_var: _descr3;;

			=> nrel_goto: .proc_find_graph_info_operator22;;
		*);;

		-> .proc_find_graph_info_operator22
		(*
			<- waitReturn;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _descr3;;

			=> nrel_goto: .proc_find_graph_info_operator17;;
		*);;

		-> .proc_find_graph_info_operator23
		(*
			<- eraseEl;;
            -> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _init_struct_copy;;

			=> nrel_goto: .proc_find_graph_info_operator4;;
		*);;

		-> .proc_find_graph_info_operator_return(*<-return;;*);;
	*);;
*);;